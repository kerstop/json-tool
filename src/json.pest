WHITESPACE = _{ (" " | "\t" | NEWLINE) }

value       =  { (object | array | string | number | boolean | null) }
object      =  { "{" ~ (object_pair ~ ("," ~ object_pair)*)? ~ "}" }
object_pair =  { string ~ ":" ~ value }
array       =  { "[" ~ (value ~ ("," ~ value)*)? ~ "]" }
number      = @{
    "-"? ~  // sign
  ("0" | '1'..'9' ~ ASCII_DIGIT*) ~  // mantissa
  ("." ~ ASCII_DIGIT*)? ~  // fraction
  (("E" | "e") ~ ("+" | "-")? ~ ASCII_DIGIT*)?
}
boolean     =  { "true" | "false" }
null        =  { "null" }

string            = ${ "\"" ~ inner_string ~ "\"" }
inner_string      = @{ inner_string_char* }
inner_string_char =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

json_file = { SOI ~ value ~ EOI }

json_path_index    =  { NUMBER* }
json_path_index_op = _{ "[" ~ json_path_index ~ "]" }
json_path_key      =  { (!("." | "[") ~ ANY)* }

/// search and parse out the keys and indecies from a json path
/// ex: hello.world[3]
json_path = ${ SOI ~ json_path_key ~ json_path_index_op* ~ ("." ~ json_path_key ~ json_path_index_op*)* ~ EOI }
